#compdef coomer

_coomer_monitors() {
  local backend=""
  local -a mons
  local i

  for i in {1..$#words}; do
    if [[ "${words[i]}" == "--backend" && $((i+1)) -le $#words ]]; then
      backend="${words[i+1]}"
      break
    fi
  done

  if [[ -n "$backend" ]]; then
    mons=(${(f)"$(coomer --backend "$backend" --list-monitors 2>/dev/null | awk '/^\[[0-9]+\]/ {print $2}')"})
  else
    mons=(${(f)"$(coomer --list-monitors 2>/dev/null | awk '/^\[[0-9]+\]/ {print $2}')"})
  fi

  _describe -t monitors 'monitor' mons
}

_arguments -s \
  '--backend[Capture backend]:mode:(auto x11 wlr portal)' \
  '--monitor[Select monitor/output by name (x11/wlr only)]:name:_coomer_monitors' \
  '--list-monitors[List monitors/outputs visible to the backend]' \
  '--overlay[Wayland layer-shell overlay]' \
  '--portal-interactive[Enable interactive mode for portal]' \
  '--no-spotlight[Disable spotlight mode]' \
  '--debug[Enable debug logging]' \
  '(-h --help)'{-h,--help}'[Show help message]'

